/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'vagrant' at '7/26/14 6:49 PM' with Gradle 1.12
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/1.12/userguide/tutorial_java_projects.html
 */

// Apply the java plugin to add support for Java
apply plugin: 'java'

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

version = '0.2.0'

configurations {
  provided
  compile.extendsFrom provided
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'maven central' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {

    // Apache Commons
    compile 'commons-codec:commons-codec:1.5'

    // Tomcat dependencies
    provided 'org.apache.tomcat:tomcat-catalina:8.0.15'
    provided 'org.apache.tomcat:tomcat-coyote:8.0.15'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.11'
}

task generateSources {
    project.ext.set("outputDir", "$projectDir/src/main/java/com/snowplowanalytics/snowplow/collectors/clojure/generated")
    doFirst {
        def srcFile = new File((String)outputDir, "ProjectSettings.java")
        srcFile.parentFile.mkdirs()
        srcFile.write(
"""/*
 * Copyright (c) 2014 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */
package com.snowplowanalytics.snowplow.collectors.clojure.generated;

public class ProjectSettings {
  public static final String VERSION = "$project.version";
  public static final String BODY_ATTRIBUTE = "com.snowplowanalytics.snowplow.body";
  public static final String DEFAULT_ENCODING = "UTF-8";
}
""")
    }
}

compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

jar {
  manifest {
    attributes 'Implementation-Title': 'SnowplowAccessValve',
      'Implementation-Version': version,
      'Built-By': System.getProperty('user.name'),
      'Built-Date': new Date(),
      'Built-JDK': System.getProperty('java.version')
  }
}

task fatJar(type: Jar) {
    dependsOn configurations.compile
    baseName = project.name + '-all'
    from {
      (configurations.compile - configurations.provided).collect {
        it.isDirectory() ? it : zipTree(it)
      }
    }
    
    with jar
}

task installFatJarInServlet(type: Copy) {
    it.dependsOn fatJar

    from 'build/libs'
    into '../java-servlet/war-resources/.ebextensions'
    include '**/' + project.name + '-all-*.jar'
}
